
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПрочитатьНастройки();
	
	УстановитьДоступностьЭлементовФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ТелеграмПодключен Тогда
		ОбновитьСостояниеБотаОтложено();
	КонецЕсли;	

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура АдресСервераТелеграмПриИзменении(Элемент)
	
	СохранитьАдресСервераТелеграм(АдресСервераТелеграм);
	
КонецПроцедуры

&НаКлиенте
Процедура АдресВебХукаПриИзменении(Элемент)
	СохранитьАдресВебХука(АдресВебХука);
КонецПроцедуры

&НаКлиенте
Процедура ТокенБотаПриИзменении(Элемент)
	СохранитьТокенБота(ТокенБота);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаполнитьАдресИзПодсказки(Команда)
	
	АдресСервераТелеграм = Элементы.АдресСервераТелеграм.ПодсказкаВвода;
	
	СохранитьАдресСервераТелеграм(АдресСервераТелеграм);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключить(Команда)
	
	ПодключитьНаСервере();
	
	ПрочитатьНастройки();
	
	УстановитьДоступностьЭлементовФормы();	
	
	ОбновитьСостояниеБотаОтложено();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСостояниеБотаКоманда(Команда)
	ОбновитьСостояниеБотаОтложено();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбновитьСостояниеБотаОтложено()
	
	СостояниеБота = НСтр("ru = 'Выполняется обновление состояния...'");
	
	ПодключитьОбработчикОжидания("ОбновитьСостояниеБота", 0.5, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСостояниеБота()
	
	Попытка
		ОтветСервера = ПолучитьСостояниеБота();
	Исключение
		СостояниеБота = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		Возврат;
	КонецПопытки;		
	
	СтрокиСообщения = Новый Массив;
	
	ШаблонСтроки = "%1: %2";
	
	Для Каждого ЭлементСтруктуры Из ОтветСервера Цикл
		
		СтрокаСообщения = СтрШаблон(ШаблонСтроки, ЭлементСтруктуры.Ключ, ЭлементСтруктуры.Значение);
		
		СтрокиСообщения.Добавить(СтрокаСообщения);
		
	КонецЦикла;
	 	
	СостояниеБота = СтрСоединить(СтрокиСообщения, Символы.ПС); 	
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСостояниеБота()
	Возврат Телеграм.ПолучитьСостояниеБота();
КонецФункции

&НаСервере
Процедура ПрочитатьНастройки()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	Константы.АдресСервераТелеграм КАК АдресСервераТелеграм,
	               |	Константы.АдресВебХукаТелеграм КАК АдресВебХука,
	               |	Константы.ТокенБотаТелеграм КАК ТокенБота,
	               |	Константы.ТелеграмПодключен КАК ТелеграмПодключен
	               |ИЗ
	               |	Константы КАК Константы";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Выборка.Следующий();
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьЭлементовФормы()
	
	Элементы.АдресВебХука.ТолькоПросмотр = ТелеграмПодключен;
	Элементы.ТокенБота.ТолькоПросмотр = ТелеграмПодключен;
	
	Элементы.Подключить.Видимость = Не ТелеграмПодключен;
	Элементы.Отключить.Видимость = ТелеграмПодключен;
	Элементы.ОбновитьСостояниеБота.Видимость = ТелеграмПодключен;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СохранитьАдресСервераТелеграм(АдресСервера)
	
	Константы.АдресСервераТелеграм.Установить(АдресСервера);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СохранитьАдресВебХука(АдресВебХука)
	
	Константы.АдресВебХукаТелеграм.Установить(АдресВебХука);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СохранитьТокенБота(ТокенБота)
	
	Константы.ТокенБотаТелеграм.Установить(ТокенБота);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПодключитьНаСервере()
	
	Телеграм.ПодключитьБота();
	
КонецПроцедуры

&НаСервере
Процедура ОтключитьНаСервере()
	Константы.ТелеграмПодключен.Установить(Ложь);
	ПрочитатьНастройки();
	УстановитьДоступностьЭлементовФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура Отключить(Команда)
	ОтключитьНаСервере();
	ЭтаФорма.ОбновитьОтображениеДанных();

КонецПроцедуры

#КонецОбласти



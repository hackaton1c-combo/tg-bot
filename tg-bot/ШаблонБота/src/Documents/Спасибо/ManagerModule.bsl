#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
// Создает новый документ "Спасибо"
//
// Параметры:
//  Кто - СправочникСсылка.ФизическиеЛица - Ссылка на пользователя, который благодарит
//  Кому - СправочникСсылка.ФизическиеЛица - Ссылка на пользователя, которого благодарят
//  ЗаЧто - Строка - Строка с текстовым описанием за что благодарность
//							
// Возвращаемое значение:
//  Структура
//  	Сообщение - Сообщение для вывода в телеграмм
//  	Команды - Массив структур (пока, всегда пустой)
//  				Наименование - Представление команды
//  				id - идентификатор команды
//
Функция СказатьСпасибо(Кто, Кому, ЗаЧто) Экспорт
	
	СтруктураВозврата = ОбщийМодульСервер.СтруктураВозвратаДляТГ_СообщениеКоманды();
	
	Док = Документы.Спасибо.СоздатьДокумент();
	Док.КтоПоблагодарил = Кто;
	Док.КогоПоблагодарил = Кому;
	Док.ЗаЧтоПоблагодарил = ЗаЧто;
	Док.Дата = ТекущаяДата();
	Попытка
		Док.Записать(РежимЗаписиДокумента.Проведение);
		СтруктураВозврата.Сообщение = "Зарегистрировано";
	Исключение
		СтруктураВозврата.Сообщение = "Что-то пошло не так :(";
		ЗаписьЖурналаРегистрации("Создание документа ""Спасибо""", УровеньЖурналаРегистрации.Ошибка,
			Метаданные.Документы.Спасибо, , ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
	Возврат СтруктураВозврата;
	
КонецФункции

// Возвращает текст с отчетом по "Спасибо" за указанный период
//
// Параметры:
// 	Дата - Дата на которую строим отчет, по умолчанию - текущая дата
//  Период - Строка - Период за который строится список:
//  	"День" - Текущий день
//  	"Вчера" - Вчерашний день
//  	"Неделя" - текущая неделя
//  	"Месяц" - текущий месяц
//  	по умолчанию - "День"
//	Формат - Строка - Вариант формата
//		"Текст" - В сообщении вернет текст с датой и содержанием
//		"Файл" - В сообщении будет ссылка на файл во временном хранилище
//								
// Возвращаемое значение:
//  Структура
//  	Сообщение - Сообщение для вывода в телеграмм
//  	Команды - Массив структур (пока, всегда пустой)
//  				Наименование - Представление команды
//  				id - идентификатор команды
//  					Пока, массив всегда пустой
//
Функция ОтчетПоСпасибо(Дата = Неопределено, Период = "День", Формат = "Текст") Экспорт
	ОбщийМодульСервер.ДатаЗапросаПоУмолчанию(Дата);
	СтруктураВозврата = ОбщийМодульСервер.СтруктураВозвратаДляТГ_СообщениеКоманды();
	
	Если Период = "Вчера" Тогда
		НачалоПериода = НачалоДня(НачалоДня(Дата) - 1);
		КонецПериода = КонецДня(НачалоДня(Дата) - 1);
	ИначеЕсли Период = "Неделя" Тогда 
		НачалоПериода = НачалоНедели(Дата);
		КонецПериода = КонецНедели(Дата);
	ИначеЕсли Период = "Месяц" Тогда 
		НачалоПериода = НачалоМесяца(Дата);
		КонецПериода = КонецМесяца(Дата);
	Иначе
		НачалоПериода = НачалоДня(Дата);
		КонецПериода = КонецДня(Дата);
	КонецЕсли;	

	Запрос = Новый Запрос;
	//Нет времени объяснять, запрос по документам..
	Запрос.Текст = "ВЫБРАТЬ
	|	ПРЕДСТАВЛЕНИЕ(Спасибо.КтоПоблагодарил) КАК КтоПоблагодарил,
	|	ПРЕДСТАВЛЕНИЕ(Спасибо.КогоПоблагодарил) КАК КогоПоблагодарил,
	|	Спасибо.ЗаЧтоПоблагодарил,
	|	Спасибо.Дата
	|ИЗ
	|	Документ.Спасибо КАК Спасибо
	|ГДЕ
	|	Спасибо.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Спасибо.Проведен";
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Формат = "Файл" Тогда
		ВернутьВФайл(СтруктураВозврата, Выборка);
	Иначе
		ВернутьВТекст(Выборка, СтруктураВозврата);		
	КонецЕсли;
	
	Возврат СтруктураВозврата;
	
КонецФункции

Процедура ВернутьВФайл(СтруктураВозврата, Выборка)
	// TODO: Переделать на красивый файл, пока топорно.
	Макет = ПолучитьМакет("МакетТабДокаСОтчетом");
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьСтрока= Макет.ПолучитьОбласть("Строка");
	ТД = Новый ТабличныйДокумент;
	ТД.Вывести(ОбластьШапка);
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ОбластьСтрока.Параметры, Выборка);
		ТД.Вывести(ОбластьСтрока);
	КонецЦикла;
	//ИмяФайла = ПолучитьИмяВременногоФайла("pdf");
	//ТД.Записать(ИмяФайла, ТипФайлаТабличногоДокумента.PDF);
	Поток = Новый ПотокВПамяти();
	ТД.Записать(Поток, ТипФайлаТабличногоДокумента.PDF);
	ДвоичныеДанные = Поток.ЗакрытьИПолучитьДвоичныеДанные(); 
	Адрес = ПоместитьВоВременноеХранилище(Новый ХранилищеЗначения(ДвоичныеДанные));
	СтруктураВозврата.Сообщение = Адрес;
КонецПроцедуры

Процедура ВернутьВТекст(Выборка, СтруктураВозврата)
	МассивСтрок = Новый Массив;
	ШаблонСтроки = "%1 - %2 поблагодарил %3 за %4";
	Пока Выборка.Следующий() Цикл
		МассивСтрок.Добавить(СтрШаблон(ШаблонСтроки, 
								Формат(Выборка.Дата, "ДФ=dd.MM;"), 
								Выборка.КтоПоблагодарил, 
								Выборка.КогоПоблагодарил, 
								Выборка.ЗаЧтоПоблагодарил));
	КонецЦикла;
	
	СтруктураВозврата.Сообщение = СтрСоединить(МассивСтрок, Символы.ПС);
КонецПроцедуры

#КонецЕсли
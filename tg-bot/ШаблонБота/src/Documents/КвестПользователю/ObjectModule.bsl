#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Процедура ОбработкаПроведения(Отказ)
	
	Движения.КвестыПользователей.Записывать = Истина;
	Движения.БалансПользователя.Записывать = Истина;
	
	Если НЕ ЗначениеЗаполнено(ДатаВыдачи) Тогда
		Движения.КвестыПользователей.Очистить();
		Движения.БалансПользователя.Очистить();
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДатаВыполнения) Тогда
		ДобавитьДвижениеПоБаллам();
		ДобавитьДвижениеПоСостоянию(Перечисления.СостоянияКвеста.Выполнен, ДатаВыполнения);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДатаВзятВРаботу) Тогда
		ДобавитьДвижениеПоСостоянию(Перечисления.СостоянияКвеста.Выполняется, ДатаВзятВРаботу);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДатаВыдачи) Тогда
		ДобавитьДвижениеПоСостоянию(Перечисления.СостоянияКвеста.Выдан, ДатаВыдачи);
	КонецЕсли;
	

	
КонецПроцедуры

Процедура ДобавитьДвижениеПоБаллам()
	Движение = Движения.БалансПользователя.ДобавитьПриход();
	Движение.Период = ДатаВыполнения;
	Движение.ФизическоеЛицо = ФизическоеЛицо;
	Движение.Золото = КНачислениюЗолото;
	Движение.Серебро = КНачислениюСеребро;
КонецПроцедуры

Процедура ДобавитьДвижениеПоСостоянию(Состояние, Дата)
	Движение = Движения.КвестыПользователей.Добавить();
	Движение.Период = Дата;
	Движение.ФизическоеЛицо = ФизическоеЛицо;
	Движение.Квест = Квест;	
	Движение.Состояние = Состояние;
КонецПроцедуры

#КонецЕсли
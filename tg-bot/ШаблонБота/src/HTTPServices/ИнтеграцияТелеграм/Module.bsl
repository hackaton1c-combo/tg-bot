
Функция PingЛюбой(Запрос)
	Возврат HTTPСервисы.СформироватьHTTPОтвет(200, "ok",);
КонецФункции

Функция sendPOST(Запрос)
	
	Попытка
		
		ЗаписатьНеобработанноеСообщение(Запрос);
				
		Токен 			= Запрос.ПараметрыURL["token"];
		ДанныеЗапроса 	= HTTPСервисы.ЗначениеИзТелаJSON(Запрос);

		Возврат Телеграм.ОбработатьЗапрос(Токен, ДанныеЗапроса);
		
	Исключение
		
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		ТекстОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
		ЗаписьЖурналаРегистрации("tg-api.sendPOST", УровеньЖурналаРегистрации.Ошибка,,,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
			
		Возврат HTTPСервисы.СообщениеОбОшибке(500, НСтр("ru = 'Внутренняя ошибка сервера'"),
			ТекстОшибки,, ТекстОшибки);
			
	КонецПопытки;
	
КонецФункции

Процедура ЗаписатьНеобработанноеСообщение(Запрос)
	
	УстановитьПривилегированныйРежим(Истина);
	
	НеобработанноеСообщение = Справочники.НеобработанноеВходящееСообщениеТелеграм.СоздатьЭлемент();
	НеобработанноеСообщение.ДатаПоступления = ТекущаяУниверсальнаяДата();
	
	ЛогЗапроса = Новый Массив;
	ЛогЗапроса.Добавить(Запрос.HTTPМетод);
	ЛогЗапроса.Добавить(Запрос.ОтносительныйURL);
	ЛогЗапроса.Добавить(Запрос.ПолучитьТелоКакСтроку());
		
	ТекстЗапроса = СтрСоединить(ЛогЗапроса, Символы.ПС); 
	
	НеобработанноеСообщение.ТекстЗапроса = ТекстЗапроса;	
	
	НеобработанноеСообщение.Записать();	
	
КонецПроцедуры
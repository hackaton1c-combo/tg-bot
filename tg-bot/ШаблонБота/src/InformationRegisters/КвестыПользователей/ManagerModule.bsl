#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// Возвращает список квестов переданного пользователя
//
// Параметры:
//  Пользователь - СправочникСсылка.ФизическиеЛица - Физическое лицо, список задач которого нужно вернуть
//  Дата - Дата - Дата на которую собираются задачи (по умолчанию, ТекущаяДата())
//	КакиеКвесты - Строка - Варианты, какие квесты вернуть:
//							- "ВРаботе" - в статусе "Выполняется"
//							- "ВыданныеНеПринятые" - в статусе "Выдан"
//							- "ВсеАктивные" - в статусе "Выполняется" или "Выдан"
//							- "Выполненные" - в статусе "Выполнен"
//							- "Все" - все задачи, которые были когда-либо
//							(по умолчанию "ВсеАктивные")
//							
// Возвращаемое значение:
//  Массив Структур - Массив со ссылками на задачи Поля:
//  		Ссылка - СправочникСсылка.Квесты - Ссылка на квест
//  		Представление - Строка - Представление ссылки для ТГ  
//
Функция ПолучитьКвестыПользователя(Пользователь, Дата = Неопределено, КакиеКвесты = "ВсеАктивные") Экспорт
	ОбщийМодульСервер.ДатаЗапросаПоУмолчанию(Дата);
	СоответствиеСтатусЭмодзи = ПолучитьСоответствиеСтатусЭмодзи();
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
				   |	КвестыПользователейСрезПоследних.Квест,
				   |	КвестыПользователейСрезПоследних.Состояние КАК Состояние,
				   |	КвестыПользователейСрезПоследних.Квест.Наименование КАК Наименование
				   |ИЗ
				   |	РегистрСведений.КвестыПользователей.СрезПоследних(&Дата, ФизическоеЛицо = &ФизическоеЛицо) КАК
				   |		КвестыПользователейСрезПоследних
				   |ГДЕ
				   |	КвестыПользователейСрезПоследних.Состояние В (&Состояния)
				   |
				   |УПОРЯДОЧИТЬ ПО
				   |	КвестыПользователейСрезПоследних.Состояние";
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("ФизическоеЛицо", Пользователь);
	Состояния = Новый СписокЗначений;
	Если КакиеКвесты = "ВРаботе" Или КакиеКвесты = "ВсеАктивные" Или КакиеКвесты = "Все" Тогда
		Состояния.Добавить(Перечисления.СостоянияКвеста.Выполняется);
	КонецЕсли;
	Если КакиеКвесты = "ВыданныеНеПринятые" Или КакиеКвесты = "ВсеАктивные" Или КакиеКвесты = "Все" Тогда
		Состояния.Добавить(Перечисления.СостоянияКвеста.Выдан);
	КонецЕсли;
	Если КакиеКвесты = "Выполненные" Или КакиеКвесты = "Все" Тогда
		Состояния.Добавить(Перечисления.СостоянияКвеста.Выполнен);
	КонецЕсли;
	Запрос.УстановитьПараметр("Состояния", Состояния);
	Выборка = Запрос.Выполнить().Выбрать();
	МассивВозврата = Новый Массив;
	ШаблонПредставления = "%1 %2";

	Пока Выборка.Следующий() Цикл
		МассивВозврата.Добавить(Новый Структура("Ссылка, Представление", Выборка.Квест, СтрШаблон(ШаблонПредставления,
			СоответствиеСтатусЭмодзи.Получить(Выборка.Состояние), Выборка.Наименование)));
	КонецЦикла;
	Возврат МассивВозврата
КонецФункции

Функция ПолучитьСоответствиеСтатусЭмодзи()
	мСоответствие = Новый Соответствие;
	мСоответствие.Вставить(Перечисления.СостоянияКвеста.Выдан, "\xF0\x9F\x95\x9C");
	мСоответствие.Вставить(Перечисления.СостоянияКвеста.Выполняется, "\xF0\x9F\x91\xB7");
	мСоответствие.Вставить(Перечисления.СостоянияКвеста.Выполнен, "	\xE2\x9C\x85");
	Возврат мСоответствие;
КонецФункции

#КонецЕсли